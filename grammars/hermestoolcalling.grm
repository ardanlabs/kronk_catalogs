root ::= free-text (tool-call-block free-text)*
free-text ::= free-char*
free-char ::= [^<\x00-\x08\x0b\x0c\x0e-\x1f] | "<" ntc-t
ntc-t  ::= [^t\x00-\x08\x0b\x0c\x0e-\x1f]  | "t" ntc-o1
ntc-o1 ::= [^o\x00-\x08\x0b\x0c\x0e-\x1f]  | "o" ntc-o2
ntc-o2 ::= [^o\x00-\x08\x0b\x0c\x0e-\x1f]  | "o" ntc-l
ntc-l  ::= [^l\x00-\x08\x0b\x0c\x0e-\x1f]  | "l" ntc-us
ntc-us ::= [^_\x00-\x08\x0b\x0c\x0e-\x1f]  | "_" ntc-c
ntc-c  ::= [^c\x00-\x08\x0b\x0c\x0e-\x1f]  | "c" ntc-a
ntc-a  ::= [^a\x00-\x08\x0b\x0c\x0e-\x1f]  | "a" ntc-l2
ntc-l2 ::= [^l\x00-\x08\x0b\x0c\x0e-\x1f]  | "l" ntc-l3
ntc-l3 ::= [^l\x00-\x08\x0b\x0c\x0e-\x1f]  | "l" ntc-gt
ntc-gt ::= [^>\x00-\x08\x0b\x0c\x0e-\x1f]
tool-call-block ::= "<tool_call>" ws function-block ws "</tool_call>"
function-block ::= "<function=" function-name ">" ws parameter-block+ "</function>"
function-name ::= [a-zA-Z_] [-a-zA-Z0-9_.:/]*
parameter-block ::= "<parameter=" parameter-name ">" ws parameter-value "</parameter>" ws
parameter-name ::= [a-zA-Z_] [-a-zA-Z0-9_.:/]*
parameter-value ::= param-char*
param-char ::= [^<\x00-\x08\x0b\x0c\x0e-\x1f] | "<" nc-slash
nc-slash ::= [^/]  | "/" nc-p
nc-p     ::= [^p]  | "p" nc-a1
nc-a1    ::= [^a]  | "a" nc-r1
nc-r1    ::= [^r]  | "r" nc-a2
nc-a2    ::= [^a]  | "a" nc-m
nc-m     ::= [^m]  | "m" nc-e1
nc-e1    ::= [^e]  | "e" nc-t
nc-t     ::= [^t]  | "t" nc-e2
nc-e2    ::= [^e]  | "e" nc-r2
nc-r2    ::= [^r]  | "r" nc-gt2
nc-gt2   ::= [^>]
ws ::= [ \t\n\r]*