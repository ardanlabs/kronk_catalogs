root ::= free-text (tool-call-block free-text)*
free-text ::= free-char*
free-char ::= [^<\x00-\x08\x0b\x0c\x0e-\x1f] | "<" ntc-t
ntc-t  ::= [^t] | "t" ntc-o
ntc-o  ::= [^o] | "o" ntc-o2
ntc-o2 ::= [^o] | "o" ntc-l
ntc-l  ::= [^l] | "l" ntc-us
ntc-us ::= [^_]
tool-call-block ::= "<tool_call>" ws function-block ws "</tool_call>"
function-block ::= "<function=" function-name ">" ws parameter-block+ "</function>"
function-name ::= [a-zA-Z_] [-a-zA-Z0-9_.:/]*
parameter-block ::= "<parameter=" parameter-name ">" ws parameter-value "</parameter>" ws
parameter-name ::= [a-zA-Z_] [-a-zA-Z0-9_.:/]*
parameter-value ::= param-char*
param-char ::= [^<\x00-\x08\x0b\x0c\x0e-\x1f] | "<" nc-slash
nc-slash ::= [^/] | "/" nc-p
nc-p     ::= [^p] | "p" nc-a
nc-a     ::= [^a] | "a" nc-r
nc-r     ::= [^r] | "r" nc-a2
nc-a2    ::= [^a] | "a" nc-m
nc-m     ::= [^m]
ws ::= [ \t\n\r]*