root ::= free-text (tool-call-block free-text)*
free-text ::= free-char*
free-char ::= [^<\x00-\x08\x0b\x0c\x0e-\x1f] | "<" ntc-start
ntc-start ::= [^tf\x00-\x08\x0b\x0c\x0e-\x1f] | "t" ntc-o | "f" nf-u
ntc-o  ::= [^o] | "o" ntc-o2
ntc-o2 ::= [^o] | "o" ntc-l
ntc-l  ::= [^l] | "l" ntc-us
ntc-us ::= [^_]
nf-u   ::= [^u] | "u" nf-n
nf-n   ::= [^n] | "n" nf-c
nf-c   ::= [^c] | "c" nf-ct
nf-ct  ::= [^t] | "t" nf-i
nf-i   ::= [^i] | "i" nf-o
nf-o   ::= [^o] | "o" nf-on
nf-on  ::= [^n] | "n" nf-eq
nf-eq  ::= [^=]
tool-call-block ::= "<tool_call>" ws function-block ws "</tool_call>"
function-block ::= "<function=" function-name ">" ws parameter-block+ "</function>"
function-name ::= [a-zA-Z_] [-a-zA-Z0-9_.:/]*
parameter-block ::= "<parameter=" parameter-name ">" ws parameter-value "</parameter>" ws
parameter-name ::= [a-zA-Z_] [-a-zA-Z0-9_.:/]*
parameter-value ::= param-char+
param-char ::= [^<\x00-\x08\x0b\x0c\x0e-\x1f] | "<" nc-slash
nc-slash ::= [^/] | "/" nc-p
nc-p     ::= [^p] | "p" nc-a
nc-a     ::= [^a] | "a" nc-r
nc-r     ::= [^r] | "r" nc-a2
nc-a2    ::= [^a] | "a" nc-m
nc-m     ::= [^m]
ws ::= [ \t\n\r]*
